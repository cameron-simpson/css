#!/bin/sed -f
#
# Tidy up Subject: lines.
# Strip multiple Re: stuff and [list] tags.
#	- Cameron Simpson <cs@zip.com.au> 14feb2002
#

:top
s/^[Ss][Uu][Bb][Jj][Ee][Cc][Tt]:[ 	]*//
t subject
n
b top

:subject
# clear the hold space
x
s/.*//
x

:strip
# strip leading whitespace
s/^[ 	][ 	]*//
t strip
# strip trailing whitespace
s/[ 	][ 	]*$//
t strip
# strip [foo] -> foo (suggestion from Patrick Shanahan)
s/^\[[ 	]*\(.*\)\]$/\1/
t strip
# strip leading [nonwhite]
s/^\[[^ 	]*\]//
t strip

# last check: a "re:"?
/^[Rr][Ee]:/!b finish
# we have a leading "re:"

# place a "re:" to the hold space
x
s/.*/Re:/
x

# strip leading "re:"
s/^[Rr][Ee]:[ 	]*//
t strip

:finish
# insert hold space at start of line ("" or "Re: " depending on what we saw)
H
x
s/\n/ /

s/^/Subject: /
