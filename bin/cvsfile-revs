#!/bin/sed -nf
#
# Report revision, date, author, antecedant directly from CVS RCS files.
#       - Cameron Simpson <cs@zip.com.au> 08apr2007
#

/^desc$/q

/^[1-9][0-9]*\.[1-9]/{
  h
  :inrev
  n
  y/	/ /
  /^$/{
    x
    s/\n/	/g
    p
    /	$/q
    b endrev
  }
  /^date /{
    s/^date  *\([789]\)/date 19\1/
    s/^date  *\([12][0-9][0-9][0-9]\)\.\([01][0-9]\)\.\([0-3][0-9]\)\.\([0-2][0-9]\)\.\([0-5][0-9]\)\.\([0-5][0-9]\);  *author \([^;]*\);.*/\1-\2-\3_\4:\5:\6 \7/
    H
    b inrev
  }
  /^next /{
    s/^next  *\([^;]*\);.*/\1/
    H
    b inrev
  }
  b inrev
}
:endrev
d
