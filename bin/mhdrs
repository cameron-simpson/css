#!/bin/sh
#
# Suck up mail item, emit header and body on one line for shell use as:
#	read hdr body
# Drops the colon from the header.
#
# The -sh option emits a bunch of shell variable settings.
# Example use:
#	eval "mhdrs -sh < mailitem | sed 's/^/hdr_/'`
# gets a bunch of values like $hdr_MESSAGE_ID for "Message-Id", etc.
#	- Cameron Simpson <cs@zip.com.au>
#

cmd=$0
usage="Usage: $cmd [-sh] <mailitem
	-sh	Emit sh parsable output."

badopts=

shmode=
if [ "x$1" = x-sh ]
then
    shmode=1; shift
fi

[ $# = 0 ] || { echo "$cmd: extra arguments: $*" >&2; badopts=1; }

[ $badopts ] && { echo "$usage" >&2; exit 2; }

sedf='	# collate continuation lines
	/^[ 	]/{
	  H
	  d
	}

	# Make "From " look like a header
	1{
	  s/^From /From_: /
	}

	# not continuation - flush held line
	1!{
	  x
	  s/\n/ /g
	  p
	  x
	}

	# done? quit
	/^$/q

	# start new line
        s/://
	h
       '

[ $shmode ] || exec sed -n "$sedf"

sed -n "$sedf" \
| sed 's/[ 	][ 	]*/\n/' \
| sed -n \
      '	y/abcdefghijklmnopqrstuvwxyz/ABCDEFGHIJKLMNOPQRSTUVWXYZ/
	s/[^0-9ABCDEFGHIJKLMNOPQRSTUVWXYZ__]/_/g
	s/^[^ABCDEFGHIJKLMNOPQRSTUVWXYZ_]/_&/
	h
	n
	/[^-a-zA-Z0-9_@]/{
          s/'\''/&\\&&/g
          s/^/'\''/
          s/$/'\''/
	}
	x
	G
	s/\n/=/
	p'
