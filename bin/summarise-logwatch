#!/usr/bin/sed -f
#
# Process Logwatch reports to strip irrelevant cruft.
# Used in my display_filter for mutt for these messages.
#       - Cameron Simpson <cs@zip.com.au> 23apr2011
#

s/^  *//
s/  *$//
/^$/d

/^#*# Logwatch [0-9].* ##*#$/,/^#*#$/{
  d
}
/^#*# Logwatch End #*#$/d

/^-*- Named Begin -*-$/,/^-*- Named End -*-$/{
  /^--/d
  /^\*\*Unmatched Entries\*\*/d
  /^network unreachable resolving '/d
  /^client .* query .* denied/d
  s/^/named: /
  p
  d
}

/^-*- postfix Begin -*-$/,/^-*- postfix End -*-$/{
  /^--/d
  /^[0-9][0-9]* messages removed from queue/d
  s/^/postfix: /
  p
  d
}

/^-*- SSHD Begin -*-$/,/^-*- SSHD End -*-$/{
  /^--/d
  /^Postponed authentication:/,/^Users logging in through sshd:/{
    /^Users logging in/!{
      d
    }
  }
  /^Users logging in through sshd:/,/^Received disconnect:/{
    /^Received disconnect:/!{
      s/^[0-9]/  &/
    }
  }
  /^Received disconnect:/,/^\*\*Unmatched Entries\*\*$/{
    /^\*\*Unmatched Entries\*\*$/!d
  }
  /^Users logging in through sshd:/d
  /^Received disconnect:/d
  /^\*\*Unmatched Entries\*\*$/d
  s/^/sshd: /
  p
  d
}

/^-*- Disk Space Begin -*-$/,/^-*- Disk Space End -*-$/{
  /^--/d
  /^Filesystem  *Size  *Used  *Avail  *Use%  *Mounted on/d
  / 100% /!{ / 9[6789]% /!d; }
  s/^/df: /
  p
  d
}

s/^/UNMATCHED: /
