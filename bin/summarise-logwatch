#!/usr/bin/sed -f
#
# Process Logwatch reports to strip irrelevant cruft.
# Used in my display_filter for mutt for these messages.
#       - Cameron Simpson <cs@zip.com.au> 23apr2011
#

s/^  *//
s/  *$//

/^#*# Log[Ww]atch [0-9].* ##*#$/,/^#*#$/{
  d
}
/^#*# Logwatch End #*#$/d

/^-*- Sudo .* Begin -*-$/,/^-*- Sudo .* End -*-$/{
  /^--/d
  /^==/d
  /^$/d
  s/^/sudo: /
  p
  d
}

/^-*- Named Begin -*-$/,/^-*- Named End -*-$/{
  /^--/d
  /^\*\*Unmatched Entries\*\*/d
  /^network unreachable resolving '/d
  /^client .* query .* denied/d
  /^$/d
  s/^/named: /
  p
  d
}

/^-*- pam_unix Begin -*-$/,/^-*- pam_unix End -*-$/{
  /^--/d
  /[a-z][a-z0-9]* -> /!d
  /^$/d
  s/^/pam_unix: /
  p
  d
}

/^-*- Cron Begin -*-$/,/^-*- Cron End -*-$/{
  /^--/d
  /^$/d
  s/^/cron: /
  p
  d
}

/^-*- [Pp]ostfix Begin -*-$/,/^-*- [Pp]ostfix End -*-$/{
  /^--/d
  /^==/d
  /^[0-9][0-9]* messages removed from queue/d
  /^Unrecognized warning:$/,/^$/{
    /^Unrecognized warning:$/d
  }
  /^\*\*Unmatched Entries\*\*$/d
  /^$/d
  s/^/postfix: /
  p
  d
}

/^-*- SSHD Begin -*-$/,/^-*- SSHD End -*-$/{
  /^--/d
  /^Illegal users from these:/,/^$/{
    /^Illegal users from these:/d
    s/^./illegal user: &/
  }
  /^Postponed authentication:/,/^$/{
    /^Postponed authentication:/d
    d
  }
  /^Users logging in through sshd:/,/^$/{
    /^Users logging in through sshd:/d
    / times$/s/^/  /
  }
  /^Received disconnect:/,/^$/{
    d
  }
  /^\*\*Unmatched Entries\*\*$/,/^$/{
    /^\*\*Unmatched Entries\*\*$/d
    /^Found matching DSA key: /d
    /^Read error from remote host /d
  }
  /^$/d
  s/^/sshd: /
  p
  d
}

/^-*- Disk Space Begin -*-$/,/^-*- Disk Space End -*-$/{
  /^--/d
  /^Filesystem  *Size  *Used  *Avail  *Use%  *Mounted on/d
  / 100% /!{ / 9[6789]% /!d; }
  /^$/d
  s/^/df: /
  p
  d
}

/^-*- Disk Space -*-$/,/^-*- Disk Space -*-$/{
  /^--/d
  /^Filesystem  *Size  *Used  *Avail  *Use%  *Mounted on/d
  / 100% /!{ / 9[6789]% /!d; }
  /^$/d
  s/^/df: /
  p
  d
}

/^-*- XNTPD Begin -*-$/,/^-*- XNTPD End -*-$/{
  /^--/d
  /^Total synchronizations /d
  /^$/d
  s/^/xntpd: /
  p
  d
}

/^-*- Connections (secure-log) Begin -*-$/,/^-*- Connections (secure-log) End -*-$/{
  /^--/d
  /^start .* from .* wd=/d
  /^end .* exit=0$/d
  /^$/d
  s/^/xntpd: /
  p
  d
}

/^$/d
s/^/UNMATCHED: /
