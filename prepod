#!/bin/sed -f
#
# Preprocess POD data to enhance it.
# So far:
#	=image => HTML IMG tag
#	=author 
#	=modtime
#	=keywords => discarded
#
#	- Cameron Simpson <cs@zip.com.au> 01jul2003
#

# unDOSify stuff
s/$//

# Relative HREFs:
# make L<./foo> into L<http://./foo>, gets undone in postpod.
s|L<\(\./[^>]*\)>|L<http://\1>|g

# hide =keywords - handled by separate process
/^=keywords/d

# inline images
/^=image/{
s|^=image[ 	][ 	]*\(.*\)|=begin html\
\
<BR><IMG SRC="\1" ALT="\1"><BR>\
\
=end html|
b done
}

# author details
/^=author/{
s|^=author[ 	][	]*\(.*\)|=begin html\
\
<SMALL>Author: <A HREF="mailto:\1">\1</A></SMALL><BR>\
\
=end html|
b done
}

# modtime
/^=modtime/{
s|^=modtime[ 	][	]*\(.*\)|=begin html\
\
<SMALL>Last modified: \1</SMALL><BR>\
\
=end html|
b done
}

:done
