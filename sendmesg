#!/usr/bin/perl -w
#
# Acts like sendmail but intercepts messages to newsgroup@usenet and
# dispatches then via NNTP, suitably rewritten. Addresses come on the
# command line.
#	- Cameron Simpson <cs@zip.com.au> 08jun2002
#

use cs::Misc;
use cs::Source;
use cs::Sink;
use cs::MIME;
use cs::Mail::Misc;
use cs::NNTP;

$::Usage="Usage: $::cmd [--] addresses...\n";

# mutt puts a "--" before any addresses when running sendmail
if (@ARGV && $ARGV[0] eq '--')
{ shift(@ARGV);
}

die $::Usage if ! @ARGV;

open(STDERR,">> $ENV{HOME}/sendmesg.out");

$::Msg = new cs::MIME (new cs::Source (FILE,STDIN));

## $::DEBUG=1;

@::Groups=();
@::Email=();

for (@ARGV)
{
  if (/\@usenet$/i)
  { push(@::Groups,$`);
  }
  else
  { push(@::Email,$_);
  }
}

if (@::Groups)
{ $::Msg->Add(NEWSGROUPS,join(',',@::Groups),SUPERCEDE);
  $::Msg->Add(PATH,"$ENV{SITENAME}!$ENV{USER}",SUPERCEDE);
}

$::Msg->ForceMsgID();

my $msgtxt = '';
$::Msg->WriteItem(new cs::Sink(SCALAR,\$msgtxt));

$::Xit=0;

if (!open(THREADS,"| bgstdin -C set-x thread-update $ENV{HOME}/var/mail/threads/*/*"))
{ warn "$::cmd: pipe to thread-update fails: $!";
  $::Xit=1;
}
else
{ print THREADS $msgtxt;
  close(THREADS);
}

if (@::Groups)
{ warn "sending news to [@::Groups]";
  my $NNTP = new cs::NNTP;
  if (! $NNTP->PostSource(new cs::Source(SCALAR,$msgtxt)))
  { warn "$::cmd: posting via $ENV{NNTPSERVER} fails\n";
    $::Xit=1;
  }
}

if (@::Email)
# dispatch via sendmail unless $NO_LOCALSENDMAIL > 0, in which case use SMTP
{ if (defined($ENV{NO_LOCALSENDMAIL}) && $ENV{NO_LOCALSENDMAIL} =~ /^[1-9]/)
  { warn "sending email to [@::Email]";
    my $src = new cs::Source (SCALAR,$msgtxt);
    if (! cs::Mail::Misc::smtpsend($src,@::Email))
    { warn "$::cmd: mailing via $ENV{SMTPSERVER} fails\n";
      $::Xit=1;
    }
  }
  else
  { if (!open(SENDMAIL,"|-", "sendmail", "-oi", @::Email))
    { warn "$::cmd: pipe to sendmail fails: $!";
      $::Xit=1;
    }
    else
    { if (! print SENDMAIL $msgtxt)
      { warn "$::cmd: print to sendmail fails: $!";
	$::Xit=1;
      }
      if (!close(SENDMAIL))
      { warn "$::cmd: sendmail fails: $!";
	$::Xit=1;
      }
    }
  }
}

exit $::Xit;
